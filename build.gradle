plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.9'
	id 'io.spring.dependency-management' version '1.0.15.RELEASE'
	id 'jacoco'
}

jacoco {
	toolVersion = "0.8.8"
//	reportsDirectory = layout.buildDirectory.dir('customJacocoReportDir')
}

group = 'cloud.ez2learn'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect'
	implementation group: 'org.webjars', name: 'jquery', version: '2.2.4'
	implementation group: 'org.webjars', name: 'bootstrap', version: '3.3.7'
	implementation 'org.seleniumhq.selenium:selenium-java:4.1.2'
	implementation 'io.github.bonigarcia:webdrivermanager:5.3.0'

}

jar {
	enabled = false
	archivesBaseName = 'calculator'
	archiveVersion = '0.0.1-SNAPSHOT'
}

jacocoTestReport {
	reports {
		html.required = true
		xml.required = false
		csv.required = false
	}
	finalizedBy 'jacocoTestCoverageVerification'
}


task e2eTest(type: Test) {
	include '**/e2eTests/**'
	exclude '**/apiTests/**'
	exclude '**/unitTests/**'
	systemProperty "calculator.url", System.getProperty("calculator.url")
	useJUnitPlatform()
}

task apiTest(type: Test) {
	include '**/apiTests/**'
	exclude '**/e2eTests/**'
	exclude '**/unitTests/**'
	systemProperty "calculator.url", System.getProperty("calculator.url")
	useJUnitPlatform()
}


//jacocoTestReport {
//	finalizedBy 'jacocoTestCoverageVerification'
//	dependsOn test // tests are required to run before generating the report
//}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			element = 'CLASS'
			limit {
				counter = 'LINE'
				value = 'COVEREDRATIO'
				minimum = 0.90
			}
			excludes = ['*.SeleniumProjApplication', '*.IndexController', '*.CalculatorController']
		}
	}
}

test {
	exclude '**/apiTests/**'
	exclude '**/e2eTests/**'
	include '**/unitTests/**'
	finalizedBy jacocoTestReport // report is always generated after tests run
	useJUnitPlatform()
}

